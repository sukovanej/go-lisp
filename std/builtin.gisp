; STD library for gisp


(defn ? (callable args...)
    ; partial application of function
    ; >> (set add-2 (? + 2)
    ; >> (add-2 1)
    ; 3
    (fn (rem-args...) (apply callable (+ args rem-args))))


(defn . (fns...)
    ; compose function
    ; when applied as ((. f1 f2 f3 f4) args) it behaves as (f4 (f3 (f2 (f1 args))))
    (defn _apply-fns (fns x)
        (if (empty fns) (first x) (_apply-fns (head fns) [(apply (last fns) x)])))
    (fn (x...) (_apply-fns fns x)))


(defn one-of (i l)
    ; is item `i` part of list `l`
    ; >> (one-of 1 [1 2 3])
    ; #t
    ; >> (one-of "h" "hello world")
    ; #t
    (if (empty l)
        #f
        (if (== i (first l))
            #t
            (one-of i (tail l)))))


(defn empty (l)
    ; is the list empty
    (== (len l) 0))


(defn prepend (item l)
    ; prepend
    ; take original item and create an new one with the item as a first item
    ; >> (set l (list 1 2 3 4))
    ; >> (prepend l 5)
    ; >> [5, 1, 2, 3, 45]
    (+ (list item) l))


(defn first (l)
    ; get the first item of the list
    ; >> (first (list 1 2 3))
    ; 1
    (item 0 l))


(defn last (l)
    ; get the last item of the list
    ; >> (last (list 1 2 3))
    ; 3
    (item (- (len l) 1) l))


(defn tail (l)
    ; get the tail of the list
    ; >> (last (list 1 2 3))
    ; [2, 3]
    (if (== (len l) 0)
        l
        (slice 1 (len l) l)))

(defn head (l)
    ; get the head of the list
    ; >> (head (list 1 2 3))
    ; [1, 2]
    (if (== (len l) 0)
        l
        (slice 0 (- (len l) 1) l)))


(defn map (p l)
    ; map the list
    ; >> (set l (list 1 2 3 4))
    ; >> (map l (fn (x) (* x x)))
    ; [1, 4, 9, 16]
    (if (empty l) [] (append (p (last l)) (map p (head l)))))


(defn split (delimiter str)
    ; split the str by delimiter
    ; >> (split "hello world" " ")
    ; ["hello" "world"]
    (defn _split (str current result)
        (if (== str "")
            (if (empty current) result (append current result))
            (if (== (first str) delimiter)
                (if (== current "")
                    (_split (tail str) "" result)
                    (_split (tail str) "" (append current result)))
                (_split (tail str) (+ current (first str)) result))))
    (_split str "" []))


(defn join (delimiter l)
    ; join list into string
    ; >> (join ", " ["hello", "world"]
    ; "hello, world"
    (set tail-l (tail l))
    (if (empty l)
        ""
        (+ (first l) (if (empty tail-l) "" delimiter) (join delimiter tail-l))))


(defn for (callable l)
    ; do side-effect on a list
    ; >> (for (println #i) (map str [1 2 3]))
    ; 1
    ; 2
    ; 3
    (if (not (empty l))
        (progn
            (callable (first l))
            (for callable (tail l)))))


(defn zip (a b)
    ; zip two lists
    ; >> (zip [1 2] [3 4])
    ; [[1 3] [2 4]]
    (if (or (empty a) (empty b))
        []
        (prepend [(first a) (first b)] (zip (tail a) (tail b)))))
